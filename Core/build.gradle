plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group 'biz.donvi.jakesRTP1'

repositories {
    mavenCentral()
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.enginehub.org/repo/' }
    maven { url 'https://repo.mikeprimm.com/'}
}

dependencies {
    implementation  project(":API")
    implementation 'io.papermc:paperlib:1.0.6'
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.0'

    testImplementation 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
        include 'plugin.yml'
    }
}

shadowJar {
    relocate 'io.papermc', 'biz.donvi.jakesRTP.libs.io.papermc'
}

artifacts {
    archives shadowJar
}

test {
    useJUnitPlatform()
}

task copyToMainOut(type: Copy) {
    dependsOn(tasks.build)
    try {
        delete file("../build-output-final/").listFiles()
    } catch (Exception ignored) {
        println "Could not delete old plugin from test server."
    }
    try {
        from file("build/libs/Core-${project.version}-all.jar")
        into file("../build-output-final")
        rename 'Core-', 'JakesRTP-v'
        rename '-all', ''
    } catch (Exception ignored) {
        println "sError in copyToMainOut(); Do you have a test server?"
//        ignored.printStackTrace() // Uncomment for stack trace
    }
}

task prepareTestServer(type: Copy) {
    dependsOn(tasks.copyToMainOut)
    delete file("../testServer/plugins/JakesRTP.jar")
    from file("../build-output-final/JakesRTP-v${project.version}.jar")
    into file("../testServer/plugins/")
    rename '-.*\\.', '\\.'
}